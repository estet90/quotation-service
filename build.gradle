plugins {
    id "java"
    id "application"
    id "org.openapi.generator" version "4.2.3"
    id "com.google.cloud.tools.jib" version "2.0.0"
}

group "ru.kononov"
version "1.0.0"

sourceCompatibility = 11

repositories {
    mavenCentral()
    gradlePluginPortal()
}

ext {
    daggerVersion = "2.26"
    lombokVersion = "1.18.12"
}

dependencies {
    compile platform("org.apache.logging.log4j:log4j-bom:2.13.1")
    compile platform("com.fasterxml.jackson:jackson-bom:2.10.3")
    compile(
            "org.apache.logging.log4j:log4j-api",
            "org.apache.logging.log4j:log4j-core",

            "com.google.dagger:dagger:$daggerVersion",

            "commons-io:commons-io:2.6",
            "commons-cli:commons-cli:1.4",

            "com.h2database:h2:1.4.200",
            "com.zaxxer:HikariCP:3.4.2",

            "io.swagger:swagger-annotations:1.5.24",
            "org.openapitools:jackson-databind-nullable:0.2.0",
            "javax.validation:validation-api:2.0.1.Final",
            "javax.annotation:javax.annotation-api:1.3.2",
            "com.fasterxml.jackson.core:jackson-core"
    )
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    testCompile(
            "org.junit.jupiter:junit-jupiter:5.6.0",
            "org.assertj:assertj-core:3.15.0",
            "org.mockito:mockito-core:3.3.0",

            "com.google.dagger:dagger-compiler:$daggerVersion",
            "org.projectlombok:lombok:$lombokVersion"
    )
    annotationProcessor(
            "com.google.dagger:dagger-compiler:$daggerVersion",
            "org.projectlombok:lombok:$lombokVersion"
    )
}

def generatedFilesPath = "$projectDir/build/generated/sources"
def generatedOpenapiFilesPath = "$generatedFilesPath/openapi"

task generateRestApiWrap() {
    def openapiInputSpec = "$projectDir/src/main/resources/openapi/self/quotation-service.yaml"
    inputs.file("$openapiInputSpec")
    outputs.dir("$generatedOpenapiFilesPath")
    doLast {
        task generateRestApi(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
            generatorName = "spring"
            inputSpec = "$openapiInputSpec"
            outputDir = "$generatedOpenapiFilesPath"
            modelPackage = "ru.kononov.quotationservice.model"
            generateModelTests = false
            configOptions = [
                    dateLibrary  : "java8",
                    interfaceOnly: "true",
            ]
            systemProperties = [
                    models: "" //в данном случае генерируются только DTO
            ]
        }
        generateRestApi.doWork()
    }
}

sourceSets.main.java {
    srcDirs("$generatedOpenapiFilesPath/src/main/java", "$generatedFilesPath/annotationProcessor/java/main")
}

compileJava() {
    dependsOn generateRestApiWrap
}

jar {
    mainClassName = "ru.kononov.quotationservice.Application"
    manifest {
        attributes("Main-Class": mainClassName)
    }
}

test {
    useJUnitPlatform()
    dependsOn "cleanTest"
    testLogging {
        events "passed", "skipped", "failed"
    }
}
